COPY_KIT REPLACE_ME_KIT_NAME CD_NEW_KIT_NAME (
WEAPPROF ~CD_NEW_KIT_NAME 2 2 2 2 2 2 2 2 prof_fgt_9 prof_fgt_10 prof_fgt_11 prof_fgt_12 prof_fgt_13 prof_fgt_14 prof_fgt_15 prof_fgt_16 prof_fgt_17 prof_fgt_18 prof_fgt_19 prof_fgt_20 prof_fgt_21 prof_fgt_22 prof_fgt_23 prof_fgt_24 prof_fgt_25 prof_fgt_26 prof_fgt_27 prof_fgt_28 prof_fgt_29 prof_fgt_30 prof_fgt_31 prof_fgt_32 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
)

ACTION_IF GAME_IS ~bgee bg2ee iwdee eet~ THEN BEGIN

  ACTION_IF FILE_CONTAINS_EVALUATED(~clastext.2da~ ~^REPLACE_ME_KIT_NAME[ %TAB%]~) THEN BEGIN

    COPY_EXISTING ~clastext.2da~ ~override~
      REPLACE_EVALUATE ~^REPLACE_ME_KIT_NAME\([ %TAB%]+[0-9]+[ %TAB%]+\)\([0-9]+\)\(.+$\)~ BEGIN
        SPRINT ~new_clastext~ ~CD_NEW_KIT_NAME%MATCH1%%CD_NEW_KIT_NAME%%MATCH3%~
      END ~REPLACE_ME_KIT_NAME%MATCH1%%MATCH2%%MATCH3%~
      BUT_ONLY
      
    APPEND "clastext.2da" "%new_clastext%" UNLESS "CD_NEW_KIT_NAME"
        
    COPY_EXISTING ~clastext.2da~ ~override~
      PRETTY_PRINT_2DA
  
  END

  ACTION_FOR_EACH table IN backstab clsrcreq clswpbon hpclass numwslot thiefskl traplimt crippstr sneakatt BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%table%.2da~ THEN BEGIN

      ACTION_IF FILE_CONTAINS_EVALUATED(~%table%.2da~ ~^REPLACE_ME_KIT_NAME[ %TAB%]~) THEN BEGIN

        LAF fl#add_kit_ee#get_row
          STR_VAR
            file = EVAL ~%table%~ 
            row_name = REPLACE_ME_KIT_NAME
          RET
            row
        END
        OUTER_SPRINT append "CD_NEW_KIT_NAME %row%"
        APPEND "%table%.2da" "%append%" IF "REPLACE_ME_KIT_NAME"
        
        COPY_EXISTING ~%table%.2da~ ~override~
          PRETTY_PRINT_2DA
      
      END
  
    END
  
  END

  ACTION_FOR_EACH table IN clascolr clasiskl clasthac thiefscl BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%table%.2da~ THEN BEGIN

      ACTION_IF FILE_CONTAINS_EVALUATED(~%table%.2da~ ~[ %TAB%]REPLACE_ME_KIT_NAME[ %TAB%%LNL%%MNL%%WNL%]~) THEN BEGIN

        LAF fl#add_kit_ee#get_column
          STR_VAR
            file = EVAL ~%table%~ 
            column_name = REPLACE_ME_KIT_NAME
          RET
            column
        END
        OUTER_SPRINT append "$ $ CD_NEW_KIT_NAME %column%"
        APPEND_COL "%table%.2da" "%append%" IF "REPLACE_ME_KIT_NAME"
        
        COPY_EXISTING ~%table%.2da~ ~override~
          PRETTY_PRINT_2DA
      
      END
  
    END
  
  END

END

COPY_EXISTING ~d5_dual.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_EVALUATE ~OR(\([0-9]+\))\([ %TAB%%LNL%%MNL%%WNL%]+Kit(LastSummonerOf(Myself),D5FIGHT)\)~ BEGIN
      SET new_or = (%MATCH1% + 1)
    END ~OR(%new_or%) %MATCH2% Kit(LastSummonerOf(Myself),CDSK%cd_kit_count%)~
  END
  BUT_ONLY

EXTEND_TOP ~d5_dual.bcs~ ~cdtweaks/baf/d5_dual_kit.baf~ EVALUATE_BUFFER

OUTER_SET cd_kit_count += 1